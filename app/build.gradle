apply plugin: 'com.android.application'

//签名配置
Properties signingProperties = new Properties();
//检查是否需要签名
def signingFileExists = file('../signing.properties').exists()
if (!signingFileExists)
    println '当前项目被配置为不签名,如需开启,配置signing.properties文件';
else {
    println '当前项目配置了签名';
    signingProperties.load(file('../signing.properties').newDataInputStream());
}

//定义开发环境和生产环境服务器地址
def String DEBUG_URL = "\"" + rootProject.ext.url.debugUrl + "\"";
def String RELEASE_URL = "\"" + rootProject.ext.url.releaseUrl + "\"";

//定义打包的时间
def packageTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"));
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        manifestPlaceholders = [app_name: "@string/app_name"]
        buildConfigField "String", "SERVER_URL", DEBUG_URL
    }

    //先判断需不需要签名
    if (signingFileExists) {
        //签名配置
        signingConfigs {
            release {
                storeFile file(signingProperties.get('RELEASE_STORE_FILE'))
                storePassword signingProperties.get('RELEASE_STORE_PASSWORD')
                keyAlias signingProperties.get('RELEASE_KEY_PASSWORD')
                keyPassword signingProperties.get('RELEASE_KEY_ALIAS')
            }
        }
    }

    buildTypes {
        release {
            //生产环境的服务器地址
            buildConfigField "String", "SERVER_URL", RELEASE_URL
            //是否开启混淆
            minifyEnabled false
            //是否zip优化
            zipAlignEnabled true
            //移除无用的资源文件
            shrinkResources true
            //配置签名
            if (signingFileExists) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        //开发环境
        dev {
            manifestPlaceholders = [app_name: '@string/app_name']
        }
    }


    sourceSets {
        main {
            //so包可以直接放在libs目录
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    // 删掉没有签名的APK文件，同时在这里也可以配置APK文件的输出路径
    applicationVariants.all { variant ->
        variant.assemble.doLast {
            variant.outputs.each { output ->
                println "aligned " + output.outputFile
                println "unaligned " + output.packageApplication.outputFile

                File unaligned = output.packageApplication.outputFile;
                File aligned = output.outputFile
                if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    println "deleting " + unaligned.getName()
                    unaligned.delete()
                }
            }
        }
    }


}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile project(':eq')
}

